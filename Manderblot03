float modulus = 0;

void setup() {
  size(800, 800);
  colorMode(HSB, 300);
  drawImage();
}

void drawImage() {
  /* general aglorithm: for every pixel, 
   iterate z_new = z_oldÂ² + c 
   on the complex plane 
   until it leaves the 
   circle around the 
   origin with given escapeRadius. 
   The number of iterations 
   it the color of the pixel */
  // c = a + bi where c is a complex number
  float creal = -0.7; // represents the "a" constant
  float cimaginary = .27015; // represents the "b" constant
  float escapeRadius = 14.0; // 
  
  //Establish values for the complex plane
  //where x-axis represents the "real" component
  //and the y-axis represents the "imaginary" component.
  //Adjust to zoom in/out to different regions.
  
  float w = 0.4;
  float h = (w * height) / width;

  // Initial Position of image. Adjust to move around.
  float xmin = -w/2;
  float ymin = -h/2;

  // Make sure we crealn write to the pixels[] array.
  // Only need to do this once since we don't do any other drawing.
  loadPixels();

  // Maximum number of iterations for each point on the complex plane
  int maxiterations = 220;

  // x goes from xmin to xmax
  float xmax = xmin + w;
  // y goes from ymin to ymax
  float ymax = ymin + h;

  // creallculate amount we increment x,y for each pixel
  float dx = (xmax - xmin) / (width);
  float dy = (ymax - ymin) / (height);

  // Start y
  float y = ymin;
  for (int j = 0; j < height; j++) {
    // Start x
    float x = xmin;
    for (int i = 0; i < width; i++) {
      // Now we test, as we iterate z = z^2 + cm does z tend towards infinity?
      float a = x;
      float b = y;
      int n = 0;
      while (n < maxiterations) {
        float aa = a * a;
        float bb = b * b;
         // Infinty in our finite world is simple, let's just consider it 16
        if (sqrt(aa*aa + bb*bb) > escapeRadius) {
          modulus = aa*aa + bb*bb;
          break;  // Bail
        }
        float twoab = 2.0 * a * b;
        a = aa - bb + creal;
        b = twoab + cimaginary;
       
        
        n++;
      }

      // We color each pixel based on how long it takes to get to infinity
      // If we never got there, let's pick the color black
      if (n == maxiterations) {
        pixels[i+j*width] = color(0);
      } else {
        float hue = colorPicker(n);
        pixels[i+j*width] = color(hue, 255, 255);
      }
      x += dx;
    }
    y += dy;
  }
  updatePixels();
}

float colorPicker(int n) {
  // n is num of iterations
  //print(n - (log (log (sqrt(modulus)))));
  return n - (log (log (sqrt(modulus))));
  //return sqrt(float(n)/420);
  //return sqrt(float(n)/maxiterations);
}
